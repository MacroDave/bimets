MODEL
COMMENT> Test Oz Macro Model in R bimets package

COMMENT> Imports
BEHAVIORAL> M
TSRANGE 1986 2 2019 3
EQ> TSDELTALOG(M,1) = c1+c2*(LOG(TSLAG(M,1))-LOG(TSLAG(IAD,1)))
            +c3*1/4*(LOG(TSLAG(PM,1))-LOG(TSLAG(PDFD,1))+LOG(TSLAG(PM,2))-LOG(TSLAG(PDFD,2))+LOG(TSLAG(PM,3))-LOG(TSLAG(PDFD,3))+LOG(TSLAG(PM,4))-LOG(TSLAG(PDFD,4)))
            +c4*TSDELTALOG(IAD,1)
            +c5*TSLAG(TSDELTALOG(IAD,1),1)
            +c6*TSLAG(TSDELTALOG(IAD,1),2)
            +c7*(TSLAG(TDLLPOP,1) + TSLAG(TDLLPOP,1) + TSLAG(TDLLA,1))
            +c8*(LOG(TSLAG(PM,1)/TSLAG(PDFD,1))-LOG(TSLAG(PM,2)/TSLAG(PDFD,2)))
            +c9*PC_TREND
COEFF> c1 c2 c3 c4 c5 c6 c7 c8 c9
RESTRICT> c4 + c5 + c6 + c7 = 1

COMMENT> Government Consumption
BEHAVIORAL> GC
TSRANGE 1985 3 2019 3
EQ> TSDELTALOG(GC,1) = c1+c2*(LOG(TSLAG(GC,1))-LOG(TSLAG(Y,1)/(1-2*TSLAG(LURGAP,1)/100)))
                      +c3*TSLAG(TSDELTALOG(GC,1),1) + c4*(TDLLA + TDLLHPP + TDLLPOP)
COEFF> c1 c2 c3 c4 
RESTRICT> c3+c4=1

COMMENT> Government Investment
BEHAVIORAL> GI
TSRANGE 1986 1 2019 3
EQ> TSDELTALOG(GI,1) = c1+c2*(LOG(TSLAG(GI,1))-LOG(TSLAG(Y,1)/(1-2*TSLAG(LURGAP,1)/100)))
                      +c3*TSLAG(TSDELTALOG(GI,1),1) + c4*(TDLLA + TDLLHPP + TDLLPOP)
COEFF> c1 c2 c3 c4
RESTRICT> c3+c4=1

COMMENT> Non-mining Investment
BEHAVIORAL> IBN
TSRANGE 1990 2 2019 3
EQ> TSDELTALOG(IBN,1) = c1+c2*(LOG(TSLAG(IBN,1))-LOG(TSLAG(GNE,1))+0.4*LOG((TSLAG(IBCR,1)*TSLAG(PGNE,1)/TSLAG(PIBN,1))) + LOG(TSLAG(PIBN,1)/TSLAG(PGNE,1))-LOG(TSLAG(TDLLA,1)+TSLAG(TDLLPOP,1)+TSLAG(TDLLHPP,1)+TSLAG(IBNDR,1)/100))
+c3*(-LOG(TSLAG(NIBRE,1)/TSLAG(NY,1)))  
+c4*LOG(TSLAG(GNE,1)/TSLAG(GNE,3))+c8*D_OLY
COEFF> c1 c2 c3 c4 c8
RESTRICT>c4=0.5

COMMENT> Mining Investment
BEHAVIORAL> IBRE
TSRANGE 1988 2 2019 3
EQ> TSDELTALOG(IBRE,1) = c1 + c2*( LOG(TSLAG(IBRE,1)) - LOG(TSLAG(Y,1)) + LOG(TSLAG(PIBRE,1)) - LOG(TSLAG(PGNE,1)) - LOG(TSLAG(TDLLA,1) +TSLAG(TDLLPOP,1) + TSLAG(TDLLHPP,1) + TSLAG(IBREDR,1)/100) )
                  + c3*(LOG(TSLAG(PXRE,1)) - LOG(TSLAG(PGNE,1))) 
                  + c5*(LOG(PXRE)-LOG(TSLAG(PXRE,4)) - LOG(PGNE)+LOG(TSLAG(PGNE,4)))/4  
                  + c6*LOG(TSLAG(IBRE,1)/TSLAG(IBRE,3))/2 
                  + c7*(TSLAG(TDLLA,1) + TSLAG(TDLLPOP,1) + TSLAG(TDLLHPP,1) )  
                  + c8*D_IBRE_1 + c9*D_IBRE_2 + c10*D_IBRE_3 + c11*D_IBRE_4 + c12*D_IBRE_5
COEFF> c1 c2 c3 c5 c6 c7 c8 c9 c10 c11 c12
RESTRICT>c6+c7=1


COMMENT> Dwelling Investment
BEHAVIORAL> ID
TSRANGE 1987 2 2019 3
EQ> TSDELTALOG(ID,1) = c1 + c2*(LOG(TSLAG(ID,1)) - LOG(TSLAG(RC,1)) + 0.3*(TSLAG(NMR,1)-TSLAG(PI_E,1))/100 + LOG(TSLAG(PID,1)/TSLAG(PC,1))) 
      + c3*D_GSTDEC+c4*D_GSTSEP+c5*D_GSTJUN+c6*D_GSTMAR 
      + c7*(LOG(TSLAG(RPH,1)/TSLAG(RPH,4)))/3 
      + c8*(TSLAG(NMR,1)-TSLAG(NMR,5))/400 
      + c9*(TSLAG(TDLLPOP,1) + TSLAG(TDLLHPP,1) + TSLAG(TDLLA,1))
COEFF> c1 c2 c3 c4 c5 c6 c7 c8 c9
RESTRICT>c9=1

COMMENT> Stocks
BEHAVIORAL> KV
TSRANGE 1982 1 2019 3
EQ> TSDELTALOG(KV,1)=c1+c2*(LOG(TSLAG(KV,1))-LOG(TSLAG(Y,1)))
              +c3*TSLAG(TSDELTALOG(KV,1),1)+c4*(TDLLA + TDLLHPP + TDLLPOP)
              + c5*PC_TREND
COEFF> c1 c2 c3 c4 c5
RESTRICT>c3+c4=1
RESTRICT>c2=-0.001

COMMENT> Employment
BEHAVIORAL> LE
TSRANGE 1980 1 2019 3
EQ> TSDELTALOG(LE,1) = c1 + c2*(LOG(TSLAG(LE,1)) - LOG(TSLAG(Y,1))+0.4*(LOG(TSLAG(RLC,1)) - TSLAG(TLLA,1)) + TSLAG(TLLA,1) + TSLAG(TLLHPP,1) ) 
+ c3*TSLAG(TSDELTALOG(LE,1),1) 
+ c4*(TSLAG(TSDELTALOG(Y,1),1)-(TSLAG(TDLLA,1)+TSLAG(TDLLPOP,1)+TSLAG(TDLLHPP,1)))
+ c5*(TSLAG(TDLLPOP,1)) 
+ c6*(TSDELTALOG(RLC,1) - TSLAG(TDLLA,1)) 
+ c7*(TSDELTALOG(Y,1) - TSLAG(TDLLA,1) - TSLAG(TDLLPOP,1) - TSLAG(TDLLPOP,1)) 
+ c8*D_LE   
COEFF> c1 c2 c3 c4 c5 c6 c7 c8
RESTRICT>c3+c5=1

COMMENT> Employment Rate
BEHAVIORAL> LUR
TSRANGE 1964 2 2019 3
EQ> TSDELTA(LUR,1) = c1*(LOKLAG*(TSDELTA(TSLAG(LUR,1)))- LUR_DUM*0.025*(TSLAG(LUR,2)-TSLAG(TLUR,1))) 
                    + c2*( ((LOG(Y)-LOG(TSLAG(Y,2)))/2) - TY )
                    + c3*( (LOG(TSLAG(RULC,2))-LOG(TSLAG(RULC,4)))/2 )
COEFF> c1 c2 c3
RESTRICT>c1=1

COMMENT> 10 Year Bond Rate
BEHAVIORAL> N10R
TSRANGE 1993 1 2019 3
EQ> N10R = c1*TSLAG(N10R,1)
            +c2*(RSTAR+PI_E+0.25*(NCR-PI_E-RSTAR))
            +c3
COEFF> c1 c2 c3
RESTRICT> c1+c2 = 1

COMMENT> 2-Year Bond Rate
BEHAVIORAL> N2R
TSRANGE 1993 1 2019 3
EQ> N2R= c1 + c2*(RSTAR+PI_E+0.52*(NCR-RSTAR-PI_E))
            +c3*TSLAG(N2R,1)
COEFF> c1 c2 c3
RESTRICT> c2 + c3 = 1

COMMENT> Nominal business borrowing rate spread to the cash rate
BEHAVIORAL> NBRSP
TSRANGE 1993 1 2019 3
EQ> NBRSP = c1 + c2*TSLAG(NBRSP,1) + c3*LURGAP
COEFF> c1 c2 c3

COMMENT> Nominal household credit
BEHAVIORAL> NHC
TSRANGE 1993 1 2019 3
EQ> TSDELTALOG(NHC,1)=c1+c2*(LOG(TSLAG(NHC,1))-LOG(TSLAG(PH,1))-LOG(TSLAG(KID,2)))
            +c3*TSLAG(RMR,1)
            +c4*TSDELTALOG(TSLAG(NHC,1),1)
            +c5*TSDELTALOG(TSLAG(PH,1),1)
            +c6*TSDELTALOG(TSLAG(KID,2),1)
            +c7*TSDELTA(TSLAG(NMR,1),1)
            +c8*(PI_E/400)
COEFF> c1 c2 c3 c4 c5 c6 c7 c8
RESTRICT> c4+c6=1
RESTRICT> c4+c5+c8=1

COMMENT> Nominal household Financial assets
BEHAVIORAL> NHFA
TSRANGE 1993 1 2019 3
EQ> TSDELTALOG(NHFA,1)=c1+c2*(LOG(TSLAG(NHFA,1))-LOG(TSLAG(NGNE,1)))
                  +c3*TSDELTALOG(NGNE,1)
                  +c4*(TSLAG(TDLLA,1) + TSLAG(TDLLPOP,1) + TSLAG(TDLLPOP,1) + PI_E/400)
                  +c5*NHFA_TREND
                  +c6*PID_TREND
COEFF> c1 c2 c3 c4 c5 c6
RESTRICT>c3+c4=1

COMMENT> Household Liabilities
BEHAVIORAL> NHL
TSRANGE 1993 1 2019 3
EQ> TSDELTALOG(NHL,1)= c1+c10*LOG(TSLAG(NHL,1))
              +c2*(-LOG(TSLAG(NHC,1)))+c3*(-LOG(TSLAG(NY,1)))
              +c4*TSLAG(TSDELTALOG(NHL,1),1)
              +c5*TSDELTALOG(NHC,1)
              +c6*TSDELTALOG(NY,1)
COEFF> c1 c10 c2 c3 c4 c5 c6
RESTRICT>c2+c3-c10=0 
RESTRICT>c4+c5+c6=1

COMMENT> Nominal Household Other Income
BEHAVIORAL> NHOY
TSRANGE 1985 1 2019 3
EQ> TSDELTALOG(NHOY,1) = c1 + c2*(LOG(TSLAG(NHOY,1)) - LOG(TSLAG(NYEXCOE,1)))
                    +c3*TSDELTA(NMR,1)/100 
                    +c4*(TSLAG(TDLLA,1) + TSLAG(TDLLPOP,1) + TSLAG(TDLLPOP,1) + PI_E/400)
COEFF> c1 c2 c3 c4
RESTRICT>c4=1

COMMENT> Nominal GDP excl COE
IDENTITY> NYEXCOE
EQ> NYEXCOE = NY - NHCOE

COMMENT> Spread Between Nominal Mortgage and Cash Rate
BEHAVIORAL> NSP
TSRANGE 1998 1 2019 3
EQ> NSP = c1 + c2*D_NSP + c3*TSLAG(NSP,1)
COEFF>c1 c2 c3

COMMENT> Ownership Transfer Costs
BEHAVIORAL> OTC
TSRANGE 1986 3 2019 3
EQ> TSDELTALOG(OTC,1) = c1 + c2*(LOG(TSLAG(OTC,1)) - LOG(TSLAG(ID,1)))
              +c3*(LOG(TSLAG(POTC,1)/LOG(TSLAG(PID,1))))
              +c4*TSLAG(TSDELTA(NMR,1),1)/400
              +c5*(TSDELTALOG(PH,1)*100 - PI_TARGET/4)
              +c6*D_GSTSEP
              +c7*(TSLAG(TDLLA,1) + TSLAG(TDLLPOP,1) + TSLAG(TDLLPOP,1))
COEFF> c1 c2 c3 c4 c5 c6 c7
RESTRICT>c7=1

COMMENT> P
BEHAVIORAL> P
TSRANGE 1987 1 2019 3
EQ> TSDELTALOG(P,1) = c1*TSDELTALOG(PTM,1) 
              + c2*TSDELTALOG(POIL,1)
              + c3*TSLAG(TSDELTALOG(POIL,1),1)
COEFF> c1 c2 c3
RESTRICT> c1+c2+c3=1

COMMENT> PAE
BEHAVIORAL> PAE
TSRANGE 1997 4 2019 3
EQ> TSDELTALOG(PAE,1) = c1*(TDLLA + PI_E/400 ) 
              + c2*(TSDELTALOG(PW,1)-0.4239*TDLLA - PI_E/400)
COEFF> c1 c2
RESTRICT>c1=1

COMMENT> PC
BEHAVIORAL> PC
TSRANGE 1982 2 2019 3
EQ> TSDELTALOG(PC,1)=c1+c2*(LOG(TSLAG(PC,1))-LOG(TSLAG(PTM,1)))
                 +c3*PC_TREND/100
                 +c4*TSDELTALOG(PTM,1)
                 +c5*TSDELTALOG(PM,1)
                 +c6*D_GSTSEP
COEFF> c1 c2 c3 c4 c5 c6
RESTRICT>c4+c5=1

COMMENT> PG
BEHAVIORAL> PG
TSRANGE 1986 1 2019 3
EQ> TSDELTALOG(PG,1)=c1+c2*(LOG(TSLAG(PG,1))-LOG(TSLAG(PC,1)))
                +c3*(TSLAG(TSDELTALOG(PG,1),1))
                +c4*TSDELTALOG(PC,1)
COEFF> c1 c2 c3 c4
RESTRICT>c3+c4=1

COMMENT> PH
BEHAVIORAL> PH
TSRANGE 1988 3 2019 3
EQ> TSDELTALOG(PH,1) = c1 + c2*(LOG(TSLAG(PH,1)) - LOG(TSLAG(PRT,1)))
              +c3*TSLAG(RMR,1)
              +c4*TSLAG(TSDELTALOG(PH,1),1)
              +c5*PI_E/400
              +c6*TSLAG(TSDELTA(NMR,1),1)
COEFF> c1 c2 c3 c4 c5 c6
RESTRICT>c6=-0.003
RESTRICT>c4+c5=1

COMMENT> PIBN
BEHAVIORAL> PIBN
TSRANGE 1988 2 2019 3
EQ> TSDELTALOG(PIBN,1)=c1+c2*(LOG(TSLAG(PIBN,1))-LOG(TSLAG(PC,1)))
                +c3*PIBN_TREND_1
                +c4*PIBN_TREND_2
                +c5*TSLAG(TSDELTALOG(PIBN,1),1)
                +c6*TSLAG(TSDELTALOG(PIBN,1),2)
                +c7*TSDELTALOG(PM,1)
                +c8*D_GSTSEP
COEFF> c1 c2 c3 c4 c5 c6 c7 c8
RESTRICT>c5+c6+c7=1

COMMENT> PIBRE
BEHAVIORAL> PIBRE
TSRANGE 1990 1 2019 3
EQ> TSDELTALOG(PIBRE,1) = c1+c2*( LOG(TSLAG(PIBRE,1))-LOG(TSLAG(PC,1)) )
                    +c3*TSDELTALOG(PC,1)
                    +c4*TSLAG(TSDELTALOG(PIBRE,1),1)
                    +c5*PIBRE_DUM1 
                    +c6*PIBRE_DUM2 
                    +c7*PIBRE_DUM3
                    +c8*PIBRE_DUM4
COEFF> c1 c2 c3 c4 c5 c6 c7 c8
RESTRICT>c3+c4=1

COMMENT> PID
BEHAVIORAL> PID
TSRANGE 1985 1 2019 3
EQ> TSDELTALOG(PID,1) = c1+c2*LOG(TSLAG(PID,1))
                        +c3*(-LOG(TSLAG(PC,1)))
                        +c4*(-LOG(TSLAG(PM,1)))
                        +c5*TSLAG(TSDELTALOG(PID,1),1)
                        +c6*PI_E/400
                        +c7*D_GSTSEP
                        +c8*D_GSTDEC
                        +c9*PID_TREND/100
COEFF> c1 c2 c3 c4 c5 c6 c7 c8 c9
RESTRICT>c5+c6=1
RESTRICT>c3+c4-c2=0 

COMMENT> PM
BEHAVIORAL> PM
TSRANGE 1986 3 2019 3
EQ> TSDELTALOG(PM,1) = c1 + c2*(LOG(TSLAG(PM,1))+LOG(TSLAG(NTWI,1)))
                      +c3*(-LOG(TSLAG(WPX,1)))
                      +c4*(-LOG(TSLAG(WPOIL,1)))
                      +c6*TADP
                      +c7*TSLAG(TSDELTALOG(PM,1),1)
                      +c8*TSDELTALOG(NTWI,1)
                      +c9*TSLAG(TSDELTALOG(NTWI,1),1)
                      +c10*TSDELTALOG(WPX,1)
                      +c11*TSLAG(TSDELTALOG(WPX,1),1)
                      +c12*TSDELTALOG(POIL,1)
                      +c13*TSLAG(TSDELTALOG(POIL,1),1)
                      +c14*D_AFC1+c15*D_AFC2+c16*D_AFC3+c17*D_AFC4+c18*D_AFC5
COEFF> c1 c2 c3 c4 c6 c7 c8 c9 c10 c11 c12 c13 c14 c15 c16 c17 c18
RESTRICT>c7+c10+c11+c12+c13=1
RESTRICT>c3+c4-c2=0

COMMENT> PMCG
BEHAVIORAL> PMCG
TSRANGE 1986 3 2019 3
EQ> TSDELTALOG(PMCG,1) = c2*TSDELTALOG(PM,1) 
                        + c6*TSLAG(TSDELTALOG(PMCG,1),1) 
                        + c3*TSDELTALOG(POIL,1)
                        + c4*TSLAG(TSDELTALOG(POIL,1),1) 
                        + c5*D_CPMCG
COEFF> c2 c6 c3 c4 c5
RESTRICT>c6+c2+c3+c4=1

COMMENT> POTC
BEHAVIORAL> POTC
TSRANGE 1992 1 2019 3
EQ> TSDELTALOG(POTC,1)= c1+c2*(LOG(TSLAG(POTC,1))-LOG(TSLAG(PC,1)))
                        +c3*POTC_TREND_1
                        +c4*POTC_TREND_2
                        +c5*TSLAG(PI_E,1)/400
COEFF> c1 c2 c3 c4 c5
RESTRICT>c5=1

COMMENT> PRT
BEHAVIORAL> PRT
TSRANGE 1993 1 2019 3
EQ> TSDELTALOG(PRT,1) = c1 + c2*(LOG(TSLAG(PRT,1))-LOG(TSLAG(PC,1))-LOG(TSLAG(HCOE,4))+LOG(TSLAG(KID,5)))
            + c3*TSLAG(TSDELTALOG(PRT,1),1)
            + c4*PI_E/400
            + c5*(LOG(TSLAG(HCOE,1)/TSLAG(HCOE,3)))/2
            + c6*(TSLAG(TDLLA,1) + TSLAG(TDLLPOP,1) + TSLAG(TDLLPOP,1))
COEFF> c1 c2 c3 c4 c5 c6
RESTRICT>c5+c6=0
RESTRICT>c3+c4=1

COMMENT> PTM
BEHAVIORAL> PTM
TSRANGE 1993 1 2019 3
EQ> TSDELTALOG(PTM,1) = c1+c2*(LOG(TSLAG(PEX,1)))
        +c3*(-LOG(TSLAG(NULCBS,1)))+c4*(-LOG(TSLAG(PMCG,1)))
        +c5*TSLAG(TSDELTALOG(PTM,1),1)
        +c6*TSLAG(PI_E,1)/400
        +c7*LURGAP
COEFF> c1 c2 c3 c4 c5 c6 c7
RESTRICT>c3+c4-c2=0 
RESTRICT>c5+c6=1

COMMENT> PW
BEHAVIORAL> PW
TSRANGE 1998 1 2019 3
EQ> TSDELTALOG(PW,1)=c1*TDLLA
                    +c2*(TSLAG(LURGAP,1)/TSLAG(LUR,1))
                    +c3*TSLAG(TSDELTA(LUR,1),1)
                    +c4*PI_E/400
                    +c5*(LOG(TSLAG(PY,1))-LOG(TSLAG(PY,9)))/8
                    +c6*TSLAG(TSDELTALOG(PW,1))
COEFF> c1 c2 c3 c4 c5 c6
RESTRICT>c4+c5+c6=1

COMMENT> PXAG
BEHAVIORAL> PXAG
TSRANGE 1985 2 2019 3
EQ> TSDELTALOG(PXAG,1)=c1+c2*(LOG(TSLAG(PXAG,1))-LOG(TSLAG(WPAG,1))+LOG(TSLAG(NUSD,1)))
                +c3*TSDELTALOG(NUSD,1)
                +c4*TSDELTALOG(WPAG,1)
                +c5*TSLAG(TSDELTALOG(WPAG,1),1)
                +c6*TSLAG(PI_E,1)/400
COEFF> c1 c2 c3 c4 c5 c6
RESTRICT>c4+c5+c6=1

COMMENT> PXM
BEHAVIORAL> PXM
TSRANGE 1986 1 2019 3
EQ> TSDELTALOG(PXM,1)=c1+c2*(LOG(TSLAG(PXM,1))-LOG(TSLAG(PM,1)))
                      +c3*PXM_TREND/100
                      +c4*TSDELTALOG(PM,1)
                      +c5*TSLAG(PI_E,1)/400
COEFF> c1 c2 c3 c4 c5
RESTRICT>c4+c5=1

COMMENT> PXO
BEHAVIORAL> PXO
TSRANGE 1990 1 2019 3
EQ> TSDELTALOG(PXO,1)=c1+c2*(LOG(TSLAG(PXO,1))-LOG(TSLAG(NUSD,1)))
                +c3*(-LOG(TSLAG(PC,1)))
                +c4*(-LOG(TSLAG(WPCOM,1)))
                +c5*TSDELTALOG(PC,1)
                +c6*(TSDELTALOG(WPCOM,1)-TSDELTALOG(NUSD,1))
COEFF> c1 c2 c3 c4 c5 c6
RESTRICT>c3+c4-c2=0
RESTRICT>c5+c6=1

COMMENT> PXRE
BEHAVIORAL> PXRE
TSRANGE 1985 2 2019 3
EQ> TSDELTALOG(PXRE,1)=c1+c2*(LOG(TSLAG(PXRE,1))-LOG(TSLAG(WPCOM,1))+LOG(TSLAG(NUSD,1)))
                      +c3*TSDELTALOG(NUSD,1)
                      +c4*TSDELTALOG(WPCOM,1)
                      +c5*TSLAG(PI_E,1)/400
COEFF> c1 c2 c3 c4 c5

COMMENT> PXS
BEHAVIORAL> PXS
TSRANGE 1985 1 2019 3
EQ> TSDELTALOG(PXS,1)=c1+c2*(LOG(TSLAG(PXS,1))-LOG(TSLAG(PC,1)))
                      +c3*PXS_TREND_1/100
                      +c4*PXS_TREND_2/100
                      +c5*TSDELTALOG(PC,1)
                      +c6*TSLAG(TSDELTALOG(PXS,1),1)
COEFF> c1 c2 c3 c4 c5 c6
RESTRICT>c5+c6=1

COMMENT> RC
BEHAVIORAL> RC
TSRANGE 1989 1 2019 3
EQ> TSDELTALOG(RC,1)=c1+c2*(LOG(TSLAG(RC,1))+0.5/100*(TSLAG(RCR,1)))
              +c3*(-LOG(TSLAG(HDY,1)))
			  +c4*(-LOG(TSLAG(HNW,2)))
              +c5*TSDELTALOG(HCOE,1)
              +c6*TSLAG(TSDELTALOG(HOY,1),2)
              +c7*TSLAG(TSDELTALOG(HNW,1),1)
              +c8*(TSLAG(TDLLA,1) + TSLAG(TDLLPOP,1) + TSLAG(TDLLPOP,1))
              +c9*TSDELTA(TSLAG(LUR,2))/100
              +c10*DUM_RC
COEFF> c1 c2 c3 c4 c5 c6 c7 c8 c9 c10
RESTRICT>c5=0.15
RESTRICT>c5+c6+c7+c8=1
RESTRICT>c3+c4-c2=0

COMMENT> WPAG
BEHAVIORAL> WPAG
TSRANGE 1985 1 2019 3
EQ> TSDELTALOG(WPAG,1) = c1+c2*(LOG(TSLAG(WPAG,1))-LOG(TSLAG(WP,1)))
                          +c3*TSLAG(TSDELTALOG(WPAG,1),1)
                          +c4*PI_E/400
COEFF> c1 c2 c3 c4
RESTRICT>c3+c4=1

COMMENT> WPOIL
BEHAVIORAL> WPOIL
TSRANGE 1986 3 2019 3
EQ> TSDELTALOG(WPOIL,1) = c1+c2*(LOG(TSLAG(WPOIL,1))-LOG(TSLAG(WP,1)))
                          +c3*TSLAG(TSDELTALOG(WPOIL,1),1)
                          +c4*PI_E/400
COEFF> c1 c2 c3 c4
RESTRICT>c3+c4=1

COMMENT> WPX
BEHAVIORAL> WPX
TSRANGE 1985 1 2019 3
EQ> TSDELTALOG(WPX,1) = c1+c2*(LOG(TSLAG(WPX,1)/TSLAG(WP,1)))
                        +c3*WPX_TREND_1
                        +c4*WPX_TREND_2
                        +c5*TSLAG(TSDELTALOG(WPX,1))
                        +c6*TSDELTALOG(WP,1)
                        +c7*D_2008Q4
COEFF> c1 c2 c3 c4 c5 c6 c7
RESTRICT> c5+c6=1

COMMENT> WR2R
BEHAVIORAL> WR2R
TSRANGE 1993 1 2019 3
EQ> WR2R =  c1*WRR + c2*(TSLAG(WR2R,1)-TSLAG(WRR,1)) + c3*0.333256
COEFF> c1 c2 c3
RESTRICT>c2+c3=1
RESTRICT>c1=1

COMMENT> XAG
BEHAVIORAL> XAGEXRAIN
TSRANGE 1985 3 2019 3
EQ> TSDELTALOG(XAGEXRAIN,1) = c1+c2*(LOG(TSLAG(XAGEXRAIN,1))-LOG(TSLAG(WY,1)))
                              +c3*LOG(TSLAG(REWI,1))
                              +c4*TSLAG(TSDELTALOG(XAGEXRAIN,1),4)
                              +c5*(TSLAG(TDLLA,1) + TSLAG(TDLLPOP,1) + TSLAG(TDLLPOP,1))
                              +c6*PC_TREND
COEFF> c1 c2 c3 c4 c5 c6
RESTRICT> c4+c5=1

COMMENT> Agricultural Exports
IDENTITY> XAG
EQ> XAG = XAGEXRAIN+RAIN

COMMENT> XM
BEHAVIORAL> XM
TSRANGE 1986 2 2019 3
EQ> TSDELTALOG(XM) = c1+c2*(LOG(TSLAG(XM,1))-LOG(TSLAG(WY,1)))
                    +c3*LOG(TSLAG(REWI,1))
                    +c4*TSLAG(TSDELTALOG(WY,1),1)
                    +c5*TSLAG(TSDELTALOG(WY,1),2)
                    +c6*(TSLAG(TDLLA,1) + TSLAG(TDLLPOP,1) + TSLAG(TDLLPOP,1))
                    +c7*XS_TREND
COEFF> c1 c2 c3 c4 c5 c6 c7
RESTRICT>c5+c6=1

COMMENT> XO
BEHAVIORAL> XO
TSRANGE 1986 2 2019 3
EQ> TSDELTALOG(XO,1) = c1+c2*(LOG(TSLAG(XO,1))-LOG(TSLAG(WY,1)))
                        +c3*LOG(TSLAG(REWI,1))
                        +c4*(LOG(TSLAG(XO,1)/TSLAG(XO,3)))/2
                        +c5*(TSLAG(TDLLA,1) + TSLAG(TDLLPOP,1) + TSLAG(TDLLPOP,1))
                        +c6*XS_TREND
COEFF> c1 c2 c3 c4 c5 c6
RESTRICT>c4+c5=1

COMMENT> XRE
BEHAVIORAL> XRE
TSRANGE 1978 4 2019 3
EQ> TSDELTALOG(XRE,1) = c1+c2*(LOG(TSLAG(XRE,1))-LOG(TSLAG(KIBRE,6)))
                  +c3*TSLAG(TSDELTALOG(XRE,1),1)
                  +c4*D_RBAGOLD
                  +c5*(TSLAG(TDLLPOP,1) + TSLAG(TDLLPOP,1) + TSLAG(TDLLA,1))
COEFF> c1 c2 c3 c4 c5
RESTRICT>c3+c5=1

COMMENT> XS
BEHAVIORAL> XS
TSRANGE 1988 1 2019 3
EQ> TSDELTALOG(XS,1)=c1+c2*(LOG(TSLAG(XS,1))-LOG(TSLAG(WY,1)))
                      +c3*LOG(TSLAG(REWI,1))
                      +c4*D_OLYX
                      +c5*(LOG(WY)-LOG(TSLAG(WY,4)))/4
                      +c6*TSDELTALOG(REWI)
                      +c7*(TSLAG(TDLLPOP,1) + TSLAG(TDLLPOP,1) + TSLAG(TDLLA,1))
                      +c8*XS_TREND
COEFF> c1 c2 c3 c4 c5 c6 c7 c8
RESTRICT>c6+c7=1

COMMENT> TLLA
IDENTITY> TLLA
EQ> TLLA  = TSLAG(TLLA,1)  + TDLLA

COMMENT> TDLLA
IDENTITY> TDLLA
EQ> TDLLA  = 0.95  * TSLAG(TDLLA,1)  + ( 0.05  * 0.00372215312343764 )

COMMENT> TLLHPP
IDENTITY> TLLHPP
EQ> TLLHPP  = TSLAG(TLLHPP,1)  + TDLLHPP

COMMENT> TDLLHPP
IDENTITY> TDLLHPP
EQ> TDLLHPP  = 0.95  * TSLAG(TDLLHPP,1)

COMMENT> LHPP
IDENTITY> LHPP
EQ> LOG(LHPP)  = TLLHPP

COMMENT> TLLPOP
IDENTITY> TLLPOP
EQ> TLLPOP  = TSLAG(TLLPOP,1) + TDLLPOP

COMMENT> TDLLPOP
IDENTITY> TDLLPOP
EQ> TDLLPOP  = 0.975*TSLAG(TDLLPOP,1) + ( 0.025  * 0.003105629999639278 )

COMMENT> PI_E
IDENTITY> PI_E
EQ> PI_E  = 0.9  * TSLAG(PI_E,1)  + 0.1  * PI_TARGET

COMMENT> TY_POT
IDENTITY> TY_POT
EQ>  TY_POT  = TDLLA  + TDLLPOP  + TDLLHPP

COMMENT> HOY
IDENTITY> HOY
EQ>  HOY  = NHOY  / PC

COMMENT> NHDY
IDENTITY> NHDY
EQ>  NHDY  = NHCOE  + NHOY

COMMENT> HDY
IDENTITY> HDY
EQ>  HDY  = NHDY  / PC

COMMENT> NHS
IDENTITY> NHS
EQ> NHS  = NHDY  - NC  - KIDC

COMMENT> NHSR
IDENTITY> NHSR
EQ>  NHSR  = NHS  / (NHDY  - KIDC)

COMMENT> NHA
IDENTITY> NHA
EQ>  NHA  = NHNFA  + NHFA

COMMENT> NHNFA
IDENTITY> NHNFA
EQ> TSDELTALOG(NHNFA)  = TSDELTALOG(KID*PH)

COMMENT> NHNW
IDENTITY> NHNW
EQ>  NHNW  = NHA  - NHL

COMMENT> HNW
IDENTITY> HNW
EQ>  HNW  = NHNW  / PC

COMMENT> NHWS
IDENTITY> NHWS
EQ>  NHWS  = NHCOE  / NY

COMMENT> KIDC
IDENTITY> KIDC
EQ> TSDELTALOG(KIDC)  = 0.95*TSLAG(TSDELTALOG(KIDC,1),1)  + (1-0.95)*(TSLAG(TDLLA,1)  + TSLAG(TDLLPOP,1)  + TSLAG(TDLLHPP,1)  + PI_E  / 400)

COMMENT> RPH
IDENTITY> RPH
EQ>  RPH  = PH  / PTM

COMMENT> IDDR
IDENTITY> IDDR
EQ> IDDR  = TSLAG(IDDR,1)

COMMENT> KID
IDENTITY> KID
EQ> KID  = (1  - IDDR  / 100)*TSLAG(KID,1) + ID

COMMENT> IB
IDENTITY> IB
EQ>  IB  = IBRE  + IBN

COMMENT> IBCR
IDENTITY> IBCR
EQ>  IBCR  = (RBR  / 100  + IBNDRA  / 100)  * ((1  - IBCTR  * (IBNDRA  / 100  * (1  + N10R  / 100)  / (N10R  / 100  + IBNDRA  / 100)))  / (1  - IBCTR))  * (PIBN  / PGNE)

COMMENT> IBCTR
IDENTITY> IBCTR
EQ> IBCTR  = TSLAG(IBCTR,1)

COMMENT> KIBN
IDENTITY> KIBN
EQ> KIBN  = (1  - IBNDR  / 100)  * TSLAG(KIBN,1)  + IBN

COMMENT> IBNDR
IDENTITY> IBNDR
EQ> IBNDR  = TSLAG(IBNDR,1)

COMMENT> IBNDRA
IDENTITY> IBNDRA
EQ>  IBNDRA  = IBNDR  + TSLAG(IBNDR,1)  + TSLAG(IBNDR,2)  + TSLAG(IBNDR,3)

COMMENT> KIBRE
IDENTITY> KIBRE
EQ> KIBRE  = (1-IBREDR/100)*TSLAG(KIBRE,1)+IBRE

COMMENT> IBREDR
IDENTITY> IBREDR
EQ> IBREDR  = TSLAG(IBREDR,1)

COMMENT> V
IDENTITY> V
EQ> V  = TSDELTA(KV,1)

COMMENT> G
IDENTITY> G
EQ> G  = GI  + GC

COMMENT> X
IDENTITY> X
EQ>  X  = XM  + XAG  + XO  + XRE  + XS

COMMENT> IAD
IDENTITY> IAD
EQ> IAD  = RC^IAD_W_C  * IB^IAD_W_I  * GI^IAD_W_GI  * GC^IAD_W_GC  * X^IAD_W_X

COMMENT> PIAD
IDENTITY> PIAD
EQ>  PIAD  = PC^IAD_W_C  * PIB^IAD_W_I  * PG^IAD_W_GI  * PG^IAD_W_GC  * PX^IAD_W_X

COMMENT> ATS
IDENTITY> ATS
EQ> ATS  = TSLAG(ATS,1)

COMMENT> DFD
IDENTITY> DFD
EQ> DFD  = RC  + ID  + IB  + G  + OTC  - ATS

COMMENT> DFDX
IDENTITY> DFDX
EQ>  DFDX  = DFD  + X

COMMENT> DPFD
IDENTITY> DPFD
EQ>  DPFD  = RC  + ID  + IB  + OTC  - ATS

COMMENT> GNE
IDENTITY> GNE
EQ> GNE  = DFD  + V

COMMENT> Y
IDENTITY> Y
EQ>  Y  = RC  + ID  + IB  + G  + OTC  - ATS  + V  + X  - M  + SD

COMMENT> SD
IDENTITY> SD
EQ> SD  = TSLAG(SD,1)

COMMENT> NC
IDENTITY> NC
EQ>  NC  = PC  * RC  / 100

COMMENT> NID
IDENTITY> NID
EQ>  NID  = PID  * ID  / 100

COMMENT> NOTC
IDENTITY> NOTC
EQ>  NOTC  = POTC  * OTC  / 100

COMMENT> NIB
IDENTITY> NIB
EQ>  NIB  = NIBN  + NIBRE

COMMENT> NIBN
IDENTITY> NIBN
EQ>  NIBN  = PIBN  * IBN  / 100

COMMENT> NIBRE
IDENTITY> NIBRE
EQ>  NIBRE  = PIBRE  * IBRE  / 100

COMMENT> NG
IDENTITY> NG
EQ>  NG  = PG  * G  / 100

COMMENT> NV
IDENTITY> NV
EQ> NV  = PG  * V  / 100

COMMENT> NXRE
IDENTITY> NXRE
EQ>  NXRE  = PXRE  * XRE  / 100

COMMENT> NXM
IDENTITY> NXM
EQ>  NXM  = PXM  * XM  / 100

COMMENT> NXS
IDENTITY> NXS
EQ>  NXS  = PXS  * XS  / 100

COMMENT> NXO
IDENTITY> NXO
EQ>  NXO  = PXO  * XO  / 100

COMMENT> NXAG
IDENTITY> NXAG
EQ>  NXAG  = PXAG  * XAG  / 100

COMMENT> NX
IDENTITY> NX
EQ>  NX  = NXM  + NXS  + NXO  + NXRE  + NXAG

COMMENT> NM
IDENTITY> NM
EQ>  NM  = PM  * M  / 100

COMMENT> NATS
IDENTITY> NATS
EQ> NATS  = TSLAG(NATS,1)

COMMENT> NDFD
IDENTITY> NDFD
EQ> NDFD  = NC  + NID  + NIB  + NG  + NOTC  - NATS

COMMENT> NDPFD
IDENTITY> NDPFD
EQ>  NDPFD  = NC  + NID  + NIB  + NOTC  - NATS

COMMENT> NGNE
IDENTITY> NGNE
EQ> NGNE  = NDFD  + NV

COMMENT> NSD
IDENTITY> NSD
EQ> NSD  = TSLAG(NSD,1)

COMMENT> NY
IDENTITY> NY
EQ>  NY  = NC  + NID  + NIB  + NG  + NOTC  + NV  + NX  - NM  - NATS  + NSD

COMMENT> NHCOE
IDENTITY> NHCOE
EQ>  TSDELTALOG(NHCOE,1)  = TSDELTALOG(PAE,1)+TSDELTALOG(LE,1)+TSDELTALOG(LHPP,1)

COMMENT> HCOE
IDENTITY> HCOE
EQ>  HCOE  = NHCOE  / PC

COMMENT> NULC
IDENTITY> NULC
EQ>  TSDELTALOG(NULC,1)  = TSDELTALOG(NHCOE,1)-TSDELTALOG(Y,1)

COMMENT> NULCBS
IDENTITY> NULCBS
EQ> NULCBS  = NULC/TSLAG(NULC,1)*TSLAG(NULCBS,1)

COMMENT> RLC
IDENTITY> RLC
EQ>  RLC  = PAE  / PGNE

COMMENT> RULC
IDENTITY> RULC
EQ>  RULC  = NULC  / PGNE

COMMENT> RULCY
IDENTITY> RULCY
EQ>  RULCY  = NULC  / PY

COMMENT> PEX
IDENTITY> PEX
EQ> PEX  = TSLAG(PEX,1)*(PTM/TSLAG(PTM,1))

COMMENT> POIL
IDENTITY> POIL
EQ>  POIL  = WPOIL  / NUSD

COMMENT> PX
IDENTITY> PX
EQ>  PX  = NX  * 100  / X

COMMENT> PIB
IDENTITY> PIB
EQ>  PIB  = NIB  * 100  / IB

COMMENT> PDFD 
IDENTITY> PDFD
EQ>  PDFD  = NDFD  * 100  / DFD

COMMENT> PDPFD
IDENTITY> PDPFD
EQ>  PDPFD  = NDPFD  * 100  / DPFD

COMMENT> PGNE
IDENTITY> PGNE
EQ>  PGNE  = NGNE  * 100  / GNE

COMMENT> PY
IDENTITY> PY
EQ>  PY  = NY  * 100  / Y

COMMENT> TOT
IDENTITY> TOT
EQ>  TOT  = PX  / PM  * 100

COMMENT> LOKLAG
IDENTITY> LOKLAG
EQ> LOKLAG  = TSLAG(LOKLAG,1)

COMMENT> TY
IDENTITY> TY
EQ> TY  = TDLLA  + TDLLPOP  + TDLLHPP  + 0.95  * (TSLAG(TY,1)-TSLAG(TDLLA,1)  - TSLAG(TDLLHPP,1)  - TSLAG(TDLLPOP,1) )

COMMENT> TLUR
IDENTITY> TLUR
EQ> TLUR  = TSLAG(TLUR,1)

COMMENT> LURGAP
IDENTITY> LURGAP
EQ>  LURGAP  = LUR  - TLUR

COMMENT> LF
IDENTITY> LF
EQ>  LF  = LE  / (1  - (LUR  / 100))

COMMENT> LPR
IDENTITY> LPR
EQ> LPR  = LF  / LPOP  * 100

COMMENT> LPOP
IDENTITY> LPOP
EQ> TSDELTALOG(LPOP)  = TDLLPOP  - 0.005  * (log(TSLAG(LPOP,1))-TSLAG(TLLPOP,1))

COMMENT> NCR
IDENTITY> NCR
EQ> NCR  = 0.7  * TSLAG(NCR,1)  + 0.3  * ( RSTAR  + (PTM  / TSLAG(PTM,4)  * 100  - 100)  + (2  - 1)  * (PTM  / TSLAG(PTM,4)  * 100  - 100  - PI_TARGET)  - 2  * LURGAP)  - 2  / 2  * (LUR  - TSLAG(LUR,2))

COMMENT> RCR
IDENTITY> RCR
EQ>  RCR  = ((1  + NCR  / 100)  / (1  + PTM  / TSLAG(PTM,4)  - 1))  * 100  - 100

COMMENT> R2R
IDENTITY> R2R
EQ>  R2R  = ((1  + N2R  / 100)  / (1  + PTM  / TSLAG(PTM,4)  - 1))  * 100  - 100

COMMENT> NBR
IDENTITY> NBR
EQ>  NBR  = NCR  + NBRSP

COMMENT> RBR
IDENTITY> RBR
EQ>  RBR  = (1  + (1  - IBCTR)  * NBR  / 100)  / (TSLAG(PTM,1)/TSLAG(PTM,5))  * 100  - 100

COMMENT> NMR
IDENTITY> NMR
EQ>  NMR  = NCR  + NSP

COMMENT> RMR
IDENTITY> RMR
EQ>  RMR  = ( (1  + NMR  / 100)  / (PTM  / TSLAG(PTM,4))  - 1 )  * 100

COMMENT> RMRE
IDENTITY> RMRE
EQ>  RMRE  = NMR  - PI_E

COMMENT> RSTAR
IDENTITY> RSTAR
EQ> RSTAR  = 0.95  * TSLAG(RSTAR,1)  + 0.05  * 0.6827783123076919

COMMENT> RTWI_CONST
IDENTITY> RTWI_CONST
EQ> RTWI_CONST  = TSLAG(RTWI_CONST,1)  + (0.0005  * (TSLAG(RCR,1)  - 0.6827783123076919))  * RTWI_CONST_DUM

COMMENT> RTWI
IDENTITY> RTWI
EQ> TSDELTALOG(RTWI,1)  = RTWI_CONST  - 0.218928*(log(TSLAG(RTWI,1))  - 0.519544*log(TSLAG(TOT,1))+3.5/100  * (TSLAG(WR2SP,1)  - -0.5236049854787135))  + 0.231987  * TSDELTALOG(TOT,1)  - 5  / 100  * TSDELTA(WR2SP,1)

COMMENT> NTWI
IDENTITY> NTWI
EQ> TSDELTALOG(NTWI,1)  = TSDELTALOG(RTWI,1)  + TSDELTALOG(WP,1)  - TSDELTALOG(PTM,1)

COMMENT> NUSD
IDENTITY> NUSD
EQ> TSDELTALOG(NUSD,1)  = TSDELTALOG(NTWI,1)

COMMENT> REWI	
IDENTITY> REWI	
EQ> REWI  = TSLAG(REWI,1) * RTWI  / TSLAG(RTWI,1)

COMMENT> WY
IDENTITY> WY
EQ> TSDELTALOG(WY,1)  = 0.003105629999639278  + 0.00372215312343764  + 0.9  * (TSLAG(TSDELTALOG(WY,1),1)  - (0.003105629999639278  + 0.00372215312343764))

COMMENT> WP
IDENTITY> WP
EQ> TSDELTALOG(WP,1)  = 0.9  * TSLAG(TSDELTALOG(WP,1),1)  + 0.1  * PI_TARGET  / 400

COMMENT> WPCOM	
IDENTITY> WPCOM	
EQ> LOG(WPCOM)  = LOG(TSLAG(WPCOM,1))  + 0.2  * LOG(TSLAG(WPCOM,1)/TSLAG(WPCOM,5))/4  + 0.8  * PI_TARGET  / 400  - 0.05  * ( LOG(TSLAG(WPCOM,1))  - LOG(TSLAG(WP,1))  - LOG(.54))

COMMENT> WRR
IDENTITY> WRR
EQ> WRR  = 0.95  * TSLAG(WRR,1)  + 0.05  * -1

COMMENT> WRSP
IDENTITY> WRSP
EQ>  WRSP  = WRR  - RCR

COMMENT> WR2R
IDENTITY> WR2R
EQ> WR2R  = (0.99)  * (TSLAG(WR2R,1)  - TSLAG(WRR,1))  + WRR  + (1  - 0.99)*0.806560

COMMENT> WR2SP
IDENTITY> WR2SP
EQ>  WR2SP  = WR2R  - R2R

END